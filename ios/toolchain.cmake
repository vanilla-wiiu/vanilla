set(CMAKE_SYSTEM_NAME iOS)
set(CMAKE_OSX_DEPLOYMENT_TARGET 13.0 CACHE STRING "Minimum iOS version")

# Default to arm64 for device builds
if(NOT DEFINED CMAKE_OSX_ARCHITECTURES)
    if(DEFINED IOS_SIMULATOR AND IOS_SIMULATOR)
        execute_process(
            COMMAND uname -m
            OUTPUT_VARIABLE CMAKE_OSX_ARCHITECTURES
            OUTPUT_STRIP_TRAILING_WHITESPACE
        )
        set(CMAKE_OSX_ARCHITECTURES "${CMAKE_OSX_ARCHITECTURES}" CACHE STRING "iOS architectures")
    else()
        set(CMAKE_OSX_ARCHITECTURES "arm64" CACHE STRING "iOS architectures")
    endif()
endif()

if(DEFINED IOS_SIMULATOR AND IOS_SIMULATOR)
    set(CMAKE_OSX_SYSROOT iphonesimulator CACHE STRING "iOS SDK")
else()
    set(CMAKE_OSX_SYSROOT iphoneos CACHE STRING "iOS SDK")
endif()

execute_process(
    COMMAND xcrun --sdk ${CMAKE_OSX_SYSROOT} --show-sdk-path
    OUTPUT_VARIABLE CMAKE_OSX_SYSROOT_PATH
    OUTPUT_STRIP_TRAILING_WHITESPACE
)
execute_process(
    COMMAND xcrun --sdk ${CMAKE_OSX_SYSROOT} --find clang
    OUTPUT_VARIABLE CMAKE_C_COMPILER
    OUTPUT_STRIP_TRAILING_WHITESPACE
)
execute_process(
    COMMAND xcrun --sdk ${CMAKE_OSX_SYSROOT} --find clang++
    OUTPUT_VARIABLE CMAKE_CXX_COMPILER
    OUTPUT_STRIP_TRAILING_WHITESPACE
)
execute_process(
    COMMAND xcrun --sdk ${CMAKE_OSX_SYSROOT} --find ar
    OUTPUT_VARIABLE
    CMAKE_AR
    OUTPUT_STRIP_TRAILING_WHITESPACE
)
execute_process(
    COMMAND xcrun --sdk ${CMAKE_OSX_SYSROOT} --find ranlib
    OUTPUT_VARIABLE
    CMAKE_RANLIB
    OUTPUT_STRIP_TRAILING_WHITESPACE
)
execute_process(
    COMMAND xcrun --sdk ${CMAKE_OSX_SYSROOT} --find strip
    OUTPUT_VARIABLE
    CMAKE_STRIP
    OUTPUT_STRIP_TRAILING_WHITESPACE
)
execute_process(
    COMMAND xcrun --sdk ${CMAKE_OSX_SYSROOT} --find nm
    OUTPUT_VARIABLE
    CMAKE_NM
    OUTPUT_STRIP_TRAILING_WHITESPACE
)

set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)

set(CMAKE_XCODE_ATTRIBUTE_ENABLE_BITCODE NO)

set(IOS TRUE)
